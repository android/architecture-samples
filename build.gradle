buildscript {
    ext.kotlinVersion = '1.3.11'
    ext.navigationVersion = "2.1.0-alpha01"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Define versions in a single place
ext {
    // Sdk and tools
    // Support library and architecture components support minSdk 14 and above.
    minSdkVersion = 14
    targetSdkVersion = 28
    compileSdkVersion = 28

    // App dependencies
    androidXVersion = '1.0.0'
    androidXTestCoreVersion = '1.1.0'
    androidXTestExtKotlinRunnerVersion = '1.1.0'
    androidXTestRulesVersion = '1.1.1'
    androidXAnnotations = '1.0.1'
    androidXLegacySupport = '1.0.0'
    appCompatVersion = '1.0.2'
    archLifecycleVersion = '2.1.0-alpha03'
    archTestingVersion = '2.0.0'
    cardVersion = '1.0.0'
    coroutinesVersion = '1.1.0'
    dexMakerVersion = '2.12.1'
    espressoVersion = '3.1.1'
    fragmentVersion = '1.1.0-alpha06'
    guavaVersion = '27.0.1-android'
    hamcrestVersion = '1.3'
    junitVersion = '4.12'
    materialVersion = '1.0.0'
    mockitoVersion = '2.8.9'
    recyclerViewVersion = '1.0.0'
    robolectricVersion = '4.3-SNAPSHOT'
    roomVersion = '2.1.0-alpha06'
    rulesVersion = '1.0.1'
    timberVersion = '4.7.1'
}

/*
 * Workaround for https://code.google.com/p/android/issues/detail?id=182715
 *
 * The Android Gradle plugin is creating DataBindingExportBuildInfoTasks for the instrumentation
 * APK that generates from the app APKs layouts. This creates duplicate classes in the app and
 * instrumentation APK which leads to
 *      java.lang.IllegalAccessError: Class ref in pre-verified class resolved to unexpected implementation
 * on older devices.
 *
 * The workaround is to get the DataBindingExportBuildInfoTasks tasks for the instrumentation APK
 * and delete the files right after it creates them.
 */
// TODO remove this and add enabledForTests = true
subprojects {
    tasks.withType(com.android.build.gradle.internal.tasks.databinding.DataBindingExportBuildInfoTask) { task ->
        if (task.name.endsWith("AndroidTest")) {
            task.finalizedBy(tasks.create("${task.name}Workaround") << {
                task.output.deleteDir()
            })
        }
    }
}
